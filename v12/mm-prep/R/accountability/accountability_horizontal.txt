model{

######horizontal data
#####legislature
###legislature investigates
  for(i in 1:N){
    for(j in 1:2){
      muLC[i,j] <- betaLC[j,1] + betaLC[j,2]*(betaL[1] + betaL[2]*xiH[i])
      yLC[i,j] ~ dnorm(muLC[i,j],tauLC[j])
    }
  }
  
for(j in 1:2){
  sigmaLC[j] ~ dunif(.1,10)
  tauLC[j] <- pow(sigmaLC[j],-2)  
  betaLC[j,1:2] ~ dmnorm(b0,B0)
}  

###legislature exists
 for(i in 1:N){
    probit(pL[i]) <- betaL[1] + betaL[2]*xiH[i]
    yL[i] ~ dbern(pL[i])
  }

 betaL[1] ~ dnorm(0,1)
 betaL[2] ~ dnorm(1,1)T(0,)

#### judiciary   
###lower and/or upper investigates
  for(i in 1:N){
    for(j in 1:4){
      muJC[i,j] <- betaJC[j,1] + betaJC[j,2]*xiJ[i]
      yJC[i,j] ~ dnorm(muJC[i,j],tauJC[j])
    }
  }
  
  for(j in 1:4){
    betaJC[j,1:2]  ~ dmnorm(b0,B0)
    sigmaJC[j] ~ dunif(.1,10)
    tauJC[j] <- pow(sigmaJC[j],-2)  
  }

  for(i in 1:N){
    xiJstar[i] ~ dnorm(0,tauJ)
    xiJ[i] <- xiH[i] +xiJstar[i]
  }

  sigmaJ ~ dunif(.1,10)
  tauJ <- pow(sigmaJ,-2) 
  
####other investigative body 
###investigates
  for(i in 1:N){
      muIB[i] <- betaIB[1] + betaIB[2]*xiH[i]
      yIB[i] ~ dnorm(muIB[i],tauIB)
  }
  
  betaIB[1]  ~ dnorm(0,1)
  betaIB[2]  ~ dnorm(1,1)T(0,)
  sigmaIB ~ dunif(.1,10)
  tauIB <- pow(sigmaIB,-2)  

####exec respects constitution
  for(i in 1:N){
      muEC[i] <- betaEC[1] + betaEC[2]*xiH[i]
      yEC[i] ~ dnorm(muEC[i],tauEC)
  }
  
  betaEC[1]  ~ dnorm(0,1)
  betaEC[2]  ~ dnorm(1,1)T(0,)
  sigmaEC ~ dunif(.1,10)
  tauEC <- pow(sigmaEC,-2)  

#######horizontal and vertical accountability priors
  for(i in 1:N){
    xiH[i] ~ dnorm(0,1)
  }
  
 }