model{

####social accountability

###civil society index  
  for(i in 1:N){
    for(j in 1:3){
      muCS[i,j] <- betaCS[j,1] + betaCS[j,2]*xiC[i]
      yCS[i,j] ~ dnorm(muCS[i,j],tauCS[j])
    }
  }
  
  for(j in 1:3){
    betaCS[j,1:2]  ~ dmnorm(b0,B0)
    sigmaCS[j] ~ dunif(.1,10)
    tauCS[j] <- pow(sigmaCS[j],-2)  
  }
  
  for(i in 1:N){
    xiCstar[i] ~ dnorm(0,tauC)
    xiC[i] <- xiS[i] + xiCstar[i]
  }

    sigmaC ~ dunif(.1,10)
    tauC <- pow(sigmaC,-2)  


####media index
  for(i in 1:N){
    for(j in 1:6){
      muMV[i,j] <- betaMV[j,1] + betaMV[j,2]*xiM[i]
      yMV[i,j] ~ dnorm(muMV[i,j],tauMV[j])
    }
  }

  for(j in 1:6){
    betaMV[j,1:2]  ~ dmnorm(b0,B0)
    sigmaMV[j] ~ dunif(.1,10)
    tauMV[j] <- pow(sigmaMV[j],-2)  
  }
  
  for(i in 1:N){
    xiMstar[i] ~ dnorm(0,tauM)
    xiM[i] <- xiS[i] +xiMstar[i]
  }

    sigmaM ~ dunif(.1,10)
    tauM <- pow(sigmaM,-2)  

    
####expression index
  for(i in 1:N){
    for(j in 1:4){
      muFE[i,j] <- betaFE[j,1] + betaFE[j,2]*xiF[i]
      yFE[i,j] ~ dnorm(muFE[i,j],tauFE[j])
    }
  }

  for(j in 1:4){
    betaFE[j,1:2]  ~ dmnorm(b0,B0)
    sigmaFE[j] ~ dunif(.1,10)
    tauFE[j] <- pow(sigmaFE[j],-2)  
  }
  
  for(i in 1:N){
    xiFstar[i] ~ dnorm(0,tauF)
    xiF[i] <- xiS[i] +xiFstar[i]
  }

    sigmaF ~ dunif(.1,10)
    tauF <- pow(sigmaF,-2)  



####engaged society 
  for(i in 1:N){
    muES[i] <- betaES[1] + betaES[2]*xiS[i]
    yES[i] ~ dnorm(muES[i],tauES)
  }
  
    betaES[1]  ~ dnorm(0,1)
    betaES[2]  ~ dnorm(1,1)T(0,)
  sigmaES ~ dunif(.1,10)
  tauES <- pow(sigmaES,-2)  


#######social accountability priors
  for(i in 1:N){
     xiSstar[i] ~ dnorm(0,tauSocial)
     xiS[i] <- xi[i] + xiSstar[i]
  }

  sigmaSocial ~ dunif(.1,10)
  tauSocial <- pow(sigmaSocial,-2)  


######Horizontal accountability
#####legislature
###legislature variables
  for(i in 1:N){
    for(j in 1:2){
      muLC[i,j] <- betaLC[j,1] + betaLC[j,2]*(betaL[1] + betaL[2]*xiH[i])
      yLC[i,j] ~ dnorm(muLC[i,j],tauLC[j])
    }
  }
  
for(j in 1:2){
  sigmaLC[j] ~ dunif(.1,10)
  tauLC[j] <- pow(sigmaLC[j],-2)  
  betaLC[j,1:2] ~ dmnorm(b0,B0)
}  

###legislature exists
 for(i in 1:N){
    probit(pL[i]) <- betaL[1] + betaL[2]*xiH[i]
    yL[i] ~ dbern(pL[i])
  }

 betaL[1] ~ dnorm(0,1)
 betaL[2] ~ dnorm(1,1)T(0,)

#### judiciary index  
  for(i in 1:N){
    for(j in 1:4){
      muJC[i,j] <- betaJC[j,1] + betaJC[j,2]*xiJ[i]
      yJC[i,j] ~ dnorm(muJC[i,j],tauJC[j])
    }
  }
  
  for(j in 1:4){
    betaJC[j,1:2]  ~ dmnorm(b0,B0)
    sigmaJC[j] ~ dunif(.1,10)
    tauJC[j] <- pow(sigmaJC[j],-2)  
  }

  for(i in 1:N){
    xiJstar[i] ~ dnorm(0,tauJ)
    xiJ[i] <- xiH[i] +xiJstar[i]
  }

  sigmaJ ~ dunif(.1,10)
  tauJ <- pow(sigmaJ,-2) 
  
####other investigative body 
  for(i in 1:N){
      muIB[i] <- betaIB[1] + betaIB[2]*xiH[i]
      yIB[i] ~ dnorm(muIB[i],tauIB)
  }
  
  betaIB[1:2]  ~ dmnorm(b0,B0)
  sigmaIB ~ dunif(.1,10)
  tauIB <- pow(sigmaIB,-2)  

####exec respects constitution
  for(i in 1:N){
      muEC[i] <- betaEC[1] + betaEC[2]*xiH[i]
      yEC[i] ~ dnorm(muEC[i],tauEC)
  }
  
  betaEC[1]  ~ dnorm(0,1)
  betaEC[2]  ~ dnorm(1,1)T(0,)
  sigmaEC ~ dunif(.1,10)
  tauEC <- pow(sigmaEC,-2)  

#######horizontal accountability prior
  for(i in 1:N){
     xiHstar[i] ~ dnorm(0,tauHorizontal)
     xiH[i] <- xi[i] + xiHstar[i]
  }

  sigmaHorizontal ~ dunif(.1,10)
  tauHorizontal <- pow(sigmaHorizontal,-2)  


#######Vertical accountability

###Clean elections variables
for(i in 1:N){
  for(j in 1:7) {
    muEV[i,j] <- betaEV[j,1] + betaEV[j,2]*(betaER[1] + betaER[2]*xiV[i])
    yEV[i,j] ~ dnorm(muEV[i,j],tauEV[j])
  }
}
for(j in 1:7){
  betaEV[j,1] ~ dnorm(0,1)
  betaEV[j,2] ~ dnorm(1,1)T(0,)
  sigmaEV[j] ~ dunif(.1,10)
  tauEV[j] <- pow(sigmaEV[j],-2)   
}

###elections dichotomous
for(i in 1:N){
  probit(pER[i]) <- betaER[1] + betaER[2]*xiV[i]
  yER[i] ~ dbern(pER[i])
}

betaER[1] ~ dnorm(0,1)
betaER[2] ~ dnorm(1,1)T(0,)

###Suffrage
for(i in 1:N){
  probit(muPE[i]) <- betaPE[1] + betaPE[2]*xiV[i]
  yPE[i] ~ dbeta(muPE[i]*tauPE, (1-muPE[i])*tauPE)
}

tauPE ~ dgamma(1,1)

betaPE[1:2] ~ dmnorm(b0,B0)


###Head of executive elected
for(i in 1:N){
  probit(pO3[i])<-betaO3[1] + betaO3[2]*xiV[i]
  yO3[i] ~ dbern(pO3[i])
}

betaO3[1] ~ dnorm(0,1)
betaO3[2] ~ dnorm(1,1)T(0,)

###########Parties

for(i in 1:N){
  for(j in 1:3) {
    muP[i,j] <- betaP[j,1] + betaP[j,2]*xiP[i]
    yP[i,j] ~ dnorm(muP[i,j],tauPP[j])
  }
}
for(j in 1:3){
  betaP[j,1:2] ~ dmnorm(b0,B0)
  sigmaPP[j] ~ dunif(.1,10)
  tauPP[j] <- pow(sigmaPP[j],-2)   
}

  for(i in 1:N){
    xiPstar[i] ~ dnorm(0,tauP)
    xiP[i] <- xiV[i] +xiPstar[i]
  }
  
  sigmaP ~ dunif(.1,10)
  tauP <- pow(sigmaP,-2) 

#######vertical accountability prior
  for(i in 1:N){
     xiVstar[i] ~ dnorm(0,tauVertical)
     xiV[i] <- xi[i] + xiVstar[i]
  }
  sigmaVertical ~ dunif(.1,10)
  tauVertical <- pow(sigmaVertical,-2)  

#############Accountability

  for(i in 1:N){
    xi[i] ~ dnorm(0,1)
  }
  
}